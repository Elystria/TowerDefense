grammar xtext.TowerDefense with org.eclipse.xtext.common.Terminals

generate towerDefense "http://www.TowerDefense.xtext"

Jeu:
	'Jeu' name=ID '{'
		parties+=Partie* 
	'}';
	
Partie:
	'Partie' name=ID '{'
		//natures+=NatureTerrain*
		cases+=Case*
		cartes+=Carte*
		partieElements+=PartieElement*
		niveaux+=Niveau*
	'}'
;
	
PartieElement:
	Obstacle | Mobile | Projectile | NatureTerrain
;

Energie : 
	infini?='infini' | valeur=INT
;

Obstacle:
	'Obstacle' name=ID '{'
	'Campement' campement=[Case]
	'PVmax' quantiteEnergieMax=Energie
	'Tactique' tactique=TactiqueType
	'}'
;

enum TactiqueType:
	proche='procheFirst' | faible='faibleFirst' | fort='fortFirst'
;

Mobile:
	'Mobile' name=ID '{'
	'F' force=INT
	'V' volume=INT
	'NumVague' numVague=INT
	'In' caseEntree=[Case]
	'Out' caseSortie=[Case]
	'PVmax' quantiteEnergieMax=Energie
	'Tactique' tactique=TactiqueType
	'}'
;

Projectile:
	'Projectile' name=ID '{'
	'Portee' portee=INT
	'Masse' masse=INT
	'Vitesse' vitesse=INT
	'Position' case=[Case]
	'Direction' cible=[Case]
	'Degat' degat=Energie
	'}'	
;

NatureTerrain:
	'NatureTerrain' name=ID '{'
	'Volume' volume=INT
	'Type' type=TYPE_TERRAIN
	'FatigueEntree' fatigue=Energie
	'}'
;

enum TYPE_TERRAIN: 
	entree='entree' | sortie='sortie' | chemin='chemin'| campement='camp' | decoration='deco'
;

Niveau:
	'Niveau' name=ID difficulte=Difficulte '{'
	'DureePauses' dureePause=INT
	'Argent' argent=INT
	'MobSortisMax' mobSortisLimite=INT
	'Carte' carte=[Carte]
	vagues+=Vague*
	'}'
;

enum Difficulte:
	facile='facile' | moyen='moyen' | difficile='difficile'
;

Carte:
	'Carte' name=ID '{'
	'Lignes' nbLignes=INT
	'Colonnes' nbColonnes=INT
	'Cases' cases+=[Case]*
	'}'
;

Case :
	name=ID
	i=INT
	j=INT
	natureDuTerrain=[NatureTerrain]
;

Vague : 
	'Vague' name=ID // j'ai rajoute un nom aux vagues
	'{'
	'Mobiles' mobiles+=[Mobile]*
	'Obstacles' obstacles+=[Obstacle]*
	'Gain' gainVictoire = INT
	'}'
;

	